import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.4.0'
    id 'io.freefair.lombok' version '8.11'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'java'
}

group 'com.kyrielia'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

allprojects {
    repositories {
        mavenCentral()
    }

    bootJar {
        enabled = false
    }

    task allDeps(type: DependencyReportTask) {}
}

subprojects { subproject ->
    apply plugin: 'maven-publish'

    if (subproject.name == 'my-spring-boot-starter-bom') {
        apply plugin: 'java-platform'
    } else {
        apply plugin: 'io.freefair.lombok'
        apply plugin: 'java'
        apply plugin: 'java-library'
        apply plugin: 'com.adarshr.test-logger'

        jar {
            enabled = true
        }
    }

    dependencies {
        api platform(SpringBootPlugin.BOM_COORDINATES)
        api platform('software.amazon.awssdk:bom:2.29.29')
    }

    publishing {
        repositories {
            maven {
                name = "github"
                url = uri("https://maven.pkg.github.com/kyrielia/my-spring-boot-starter")
                credentials {
                    username = System.getenv("GITHUB_USERNAME")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }

            mavenLocal()
        }

        publications {
            "$subproject.name"(MavenPublication) {
                artifactId subproject.name
                if ('my-spring-boot-starter-bom' == subproject.name) {
                    from components.javaPlatform
                } else {
                    from components.java
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}

wrapper {
    gradleVersion = '8.11.1'
}