import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.4.0'
    id 'io.freefair.lombok' version '8.11'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'java'
}

group 'com.kyrielia'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

allprojects {
    repositories {
        mavenCentral()
    }

    bootJar {
        enabled = false
    }

    task allDeps(type: DependencyReportTask) {}
}

subprojects { subproject ->
    apply plugin: 'maven-publish'

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.adarshr.test-logger'

    dependencies {
        api platform(SpringBootPlugin.BOM_COORDINATES)
        api platform('software.amazon.awssdk:bom:2.29.29')
    }

    jar {
        enabled = true
    }

    publishing {
        publications {
            "$subproject.name"(MavenPublication) {
                artifactId subproject.name
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }

    test {
        useJUnitPlatform()
    }
}

wrapper {
    gradleVersion = '8.11.1'
}